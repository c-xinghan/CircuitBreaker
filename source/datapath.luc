module datapath#(
    SLOW_CLOCK_DIV ~ 26 : SLOW_CLOCK_DIV > 0,
    FAST_CLOCK_DIV ~ 21 : FAST_CLOCK_DIV > 0,
    FASTER_CLOCK_DIV ~ 16 : FASTER_CLOCK_DIV > 0,
    FASTEST_CLOCK_DIV ~ 11 : FASTEST_CLOCK_DIV > 0
)(
    input button,
    input clk,  // clock
    input rst,  // reset
    output round_out[32],
    output timer_out[32],
    output sound_out[32],
    output led1_out[32],
    output led2_out[32],
    output led3_out[32],
    output led4_out[32],
    output led5_out[32],
    output slow_clock_out,
    
    // debug signals
    output debug_general1[32],
    output debug_general2[32],
    output debug_general3[32],
    output debug_general4[32]
) {
    //temp variables
    sig input_alu_a[32]
    sig input_alu_b[32]
    
    alu alu
    .clk(clk){
        counter game_timer_clock(#SIZE(1),#DIV(SLOW_CLOCK_DIV), .rst(rst)) 
        counter fastclock(#SIZE(1),#DIV(FAST_CLOCK_DIV), .rst(rst)) 
        counter fasterclock(#SIZE(1),#DIV(FASTER_CLOCK_DIV), .rst(rst)) 
        counter fastestclock(#SIZE(1),#DIV(FASTEST_CLOCK_DIV), .rst(rst)) 
        
        // edge detector declaration & connection with counters
        edge_detector edge_detector_game_timer(#RISE(1), #FALL(0), .in(game_timer_clock.value))
        edge_detector edge_detector_fast(#RISE(1), #FALL(0), .in(fastclock.value))
        edge_detector edge_detector_faster(#RISE(1), #FALL(0), .in(fasterclock.value))
        edge_detector edge_detector_fastest(#RISE(1), #FALL(0), .in(fastestclock.value))
        
        random_number_generator generator(.slow_clk(fastestclock.value), .refresh(button))
        
        cu cu(
            .button(button),
            .rst(rst),
            .decrease_timer(edge_detector_game_timer.out),
            .blink_timer1(edge_detector_fast.out),
            .blink_timer2(edge_detector_faster.out),
            .blink_timer3(edge_detector_fastest.out),
            #SLOW_CLOCK_DIV(SLOW_CLOCK_DIV),
            #FAST_CLOCK_DIV(FAST_CLOCK_DIV)
        )
        
        regfile regfile(
            .we(cu.regfile_we), 
            .wa(cu.regfile_wa), 
            .ra1(cu.regfile_ra1), 
            .ra2(cu.regfile_ra2), 
            .rst(rst)
        )
    }
    always {
        // bootstrap rd1 output to CU for convenient reading operations
        cu.regfile_rd1 = regfile.rd1
        // bootstrap rd2 output to CU for convenient branching conditions
        cu.regfile_rd2 = regfile.rd2
        
        // asel mux
        case(cu.asel){
            b00  : input_alu_a = regfile.rd1
            b01  : input_alu_a = b0
            b10  : input_alu_a = b1
            b11  : input_alu_a = generator.out - (generator.out/4) * 4 + 1
            default : 
                input_alu_a = 0
        }
        
        // bsel mux
        case(cu.bsel){
            b00 : input_alu_b = regfile.rd2
            b01 : input_alu_b = b0
            b10 : input_alu_b = b1
            b11 : input_alu_b = b10
            default: //necessary to init
                input_alu_b = 0
            
        }

        // alu
        alu.a = input_alu_a
        alu.b = input_alu_b
        alu.alufn = cu.alufn
        
        // wdsel mux
        case (cu.alu_out_sel){
            b00 : regfile.data = alu.out
            b01 : regfile.data = h3C
            b10 : regfile.data = h28
            b11 : regfile.data = hF
            default : 
                regfile.data = alu.out
        }
        
        round_out = regfile.current_round
        timer_out = regfile.current_timer
        sound_out = regfile.sound
        led1_out = regfile.led1
        led2_out = regfile.led2
        led3_out = regfile.led3
        led4_out = regfile.led4
        led5_out = regfile.led5
        slow_clock_out = game_timer_clock.value
        debug_general1 = regfile.debug1
        debug_general2 = regfile.debug2
        debug_general3 = regfile.debug3
        debug_general4 = regfile.debug4
    }
}